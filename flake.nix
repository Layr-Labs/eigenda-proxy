# This flake was initially generated by fh, the CLI for FlakeHub (version 0.1.22)
# See https://zero-to-nix.com/start/init-flake/
{
  description = "eigenda-proxy flake";

  inputs = {
    nixpkgs.url = "https://flakehub.com/f/NixOS/nixpkgs/*";
    flake-schemas.url = "https://flakehub.com/f/DeterminateSystems/flake-schemas/*";
    pre-commit-hooks.url = "github:cachix/pre-commit-hooks.nix";
  };

  outputs =
    { self
    , flake-schemas
    , nixpkgs
    , pre-commit-hooks
    ,
    }:
    let
      # Helpers for producing system-specific outputs
      supportedSystems = [
        "x86_64-linux"
        "aarch64-darwin"
        "x86_64-darwin"
        "aarch64-linux"
      ];
      forEachSupportedSystem =
        f:
        nixpkgs.lib.genAttrs supportedSystems (
          system:
          f {
            pkgs = import nixpkgs { inherit system; };
          }
        );
      # TODO: might want to use https://www.tweag.io/blog/2021-03-04-gomod2nix/ instead
      # See https://jameswillia.ms/posts/go-nix-containers.html
      buildEigenDAProxy =
        pkgs:
        pkgs.buildGoModule {
          pname = "eigenda-proxy";
          version = "v0.1.0";
          src = ./.;
          # This hash will need to change when go files are changed.
          # Also, make sure to stage the changes in git before running nix flake commands;
          # see https://github.com/NixOS/nix/issues/7107
          vendorHash = "sha256-WcTqUdamYOTb70Hi7kFRh3hEzI3LYr70eN+PlpRtlcY=";
          subPackages = [
            "cmd/server"
          ];
        };
    in
    {
      # Schemas tell Nix about the structure of your flake's outputs
      # See # See https://determinate.systems/posts/flake-schemas/
      schemas = flake-schemas.schemas;

      # Used to format this very flake.nix file using `nix fmt .`
      # See https://github.com/NixOS/nixfmt?tab=readme-ov-file#nix-fmt
      formatter = forEachSupportedSystem ({ pkgs }: pkgs.nixfmt-rfc-style);

      packages = forEachSupportedSystem (
        { pkgs }:
        rec {
          eigendaProxy = buildEigenDAProxy pkgs;
          default = eigendaProxy;
        }
      );

      # Development environments
      devShells = forEachSupportedSystem (
        { pkgs }:
        {
          default =
            let
              eigenda-proxy = buildEigenDAProxy pkgs;
              # See https://github.com/cachix/git-hooks.nix?tab=readme-ov-file#flakes-support
              pre-commit-check = pre-commit-hooks.lib.${pkgs.system}.run {
                src = ./.;
                hooks = {
                  nixpkgs-fmt.enable = true;
                  golangci-lint.enable = true;
                };
              };
            in
            pkgs.mkShell {
              # Pinned packages available in the environment
              packages = with pkgs; [
                # nix stuff
                nixpkgs-fmt
                # generic tools
                curl
                jq
                git
                # golang + go tools
                go_1_22
                # need the override because golangci-lint is otherwise built with go1.23
                # which is not compatible with our pinned toolchain in go.mod
                # See https://github.com/NixOS/nixpkgs/issues/251099
                (golangci-lint.override { buildGoModule = pkgs.buildGo122Module; })
              ];

              # A hook run every time you enter the environment
              shellHook =
                pre-commit-check.shellHook
                + ''
                  echo "Welcome to the eigenda-proxy devShell!"
                '';
            };
        }
      );
    };
}
